BveTs Map 2.02:utf-8

include '..\..\BaseMap.txt';

BveEx.MapPlugin.Load('MapPluginUsing.xml');
// -- または --
// BveEx.MapPlugin.LoadAssembly('MapPlugins\MapStatementTest.dll');

Station.Load('Stations.csv');

50;
	Station['Statement1'].Put(-1, 1, -1); // 見やすさのために設置しているだけで、マップステートメントの定義には無関係です。

	// パターン 1: 基本的な例
	// BVE 組込のステートメントと同様、引数はいくつでも指定可能です。
	BveEx.User.Automatic9045.MapStatementTest.Greetings.Put();
	BveEx.User.Automatic9045.MapStatementTest.Greetings.Put('Hello world!');
	BveEx.User.Automatic9045.MapStatementTest.Greetings.Put(12345, 'Hello world!');

150;
	Station['Statement2'].Put(-1, 1, -1);

	// パターン 2: 引数に変数を渡す
	$random = rand(100);
	BveEx.User.Automatic9045.MapStatementTest.Greetings.Put('変数 $random の値は ' + $random + 'です。');

250;
	Station['Statement3'].Put(-1, 1, -1);

	// パターン 3: キーを渡す
	// BVE 組込のステートメントには例が存在しませんが、キーもいくつでも指定可能です。
	BveEx.User.Automatic9045.MapStatementTest.Greetings['The quick brown fox jumps'].Put('over the lazy dog.');
	BveEx.User.Automatic9045.MapStatementTest.Greetings[67890]['The quick brown fox jumps'].Put('over the lazy dog.');
